// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM16K.hdl

/**
 * Memory of 16K registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    // Use address[12..13] to partition RAM16K into 4 RAM4Ks,
    //  pass the value of load to the correct RAM4K:
    DMux4Way(in=load, sel=address[12..13], 
             a=ram0, b=ram1, c=ram2, d=ram3);

    // Pass address[0..11] to RAM4K to select the appropriate
    //  RAM512, then the appropriate RAM64, then the appropriate RAM8,
    //  then the appropriate register:
    RAM4K(in=in, load=ram0, address=address[0..11], out=out0);
    RAM4K(in=in, load=ram1, address=address[0..11], out=out1);
    RAM4K(in=in, load=ram2, address=address[0..11], out=out2);
    RAM4K(in=in, load=ram3, address=address[0..11], out=out3);

    // Select out from the RAM4K chosen by address[12..13]
    Mux4Way16(a=out0, b=out1, c=out2, d=out3,
              sel=address[12..13], out=out);
}